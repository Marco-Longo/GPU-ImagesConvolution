typedef struct
{
  double real;
  double imag;
} complex;


#define PI 3.14159265358979323846

kernel void matinit(global int *out, int nrows, int ncols)
{
#if 0
    int r = get_global_id(0);
    int c = get_global_id(1);
#else
    int c = get_global_id(0); //usiamo questa soluzione perchè più efficente
    int r = get_global_id(1);
#endif

    if (c >= ncols || r >= nrows)
        return;

    out[r*ncols+c] = 50;
}

kernel void fft(global int* restrict src, global complex* restrict dest,
                int u, int v, int nrows, int ncols)
{
#if 1
    const int col = get_global_id(0);
    const int row = get_global_id(1);
#else
    const int row = get_global_id(0);
    const int col = get_global_id(1);
#endif

    if(col >= ncols || row >= nrows)
        return;

    int val = src[row*ncols+col];
    double r = 2 * PI * (((u*row)/(double)nrows) + ((v*col)/(double)ncols));
    complex z = { cos(r), -sin(r) };
    complex _z = { (val * z.real), (val * z.imag) };

    dest[row*ncols+col].real = _z.real;
    dest[row*ncols+col].imag = _z.imag;
}

kernel void somma(global complex* restrict input, global complex* restrict output,
                  int numels)
{
    /* lanciato con get_global_size = N/2 */
    int idx = get_global_id(0);

    if (idx >= numels)
        return;

    double2 in_real = (double2)(input[2*idx].real, input[2*idx+1].real);
    double2 in_imag = (double2)(input[2*idx].imag, input[2*idx+1].imag);

    output[idx].real = in_real.x + in_real.y;
    output[idx].imag = in_imag.x + in_imag.y;
}

kernel void somma_x(global complex* restrict input, global complex* restrict output,
                    int numels)
{
    const int gws = get_global_size(0);
    const int el_per_wi = (numels + gws - 1)/gws;
    
    int idx = get_global_id(0);
    complex acc = {0, 0};
    
    for(int i = 0; i < el_per_wi; ++i)
    {
        int idx_r = gws*i + idx;
        if(idx_r >= numels)
            break;
        acc.real += input[idx_r].real;
        acc.imag += input[idx_r].imag;
    }
    
    output[idx].real = acc.real;
    output[idx].imag = acc.imag;
}

kernel void somma_lmem(global complex* restrict input, global complex* restrict output,
                       local complex* restrict lmem, int numels)
{
    const int gws = get_global_size(0);
    const int el_per_wi = (numels + gws - 1)/gws;
    
    int idx = get_global_id(0);
    complex acc = {0, 0};
    
    for(int i = 0; i < el_per_wi; ++i)
    {
        int idx_r = gws*i + idx;
        if(idx_r >= numels)
            break;
        acc.real += input[idx_r].real;
        acc.imag += input[idx_r].imag;
    }
    
    int lidx = get_local_id(0);
    int lws = get_local_size(0);
    
    lmem[lidx].real = acc.real;
    lmem[lidx].imag = acc.imag;
    
    for(int attivi = lws/2; attivi >= 1; attivi /= 2)
    {
        barrier(CLK_LOCAL_MEM_FENCE);
        
        if(lidx < attivi)
        {
            acc.real += lmem[lidx+attivi].real;
            acc.imag += lmem[lidx+attivi].imag;
            
            lmem[lidx].real = acc.real;
            lmem[lidx].imag = acc.imag;
        }
    }
    
    if(lidx == 0)
    {
        output[get_group_id(0)].real = acc.real;
        output[get_group_id(0)].imag = acc.imag;
    }
}
